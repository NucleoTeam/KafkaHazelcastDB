plugins {
    id 'java'
    id 'nucleodb.java-conventions'
    id 'maven-publish'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'com.nucleodb.restore'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'GitHubPackages'
        url = uri("https://maven.pkg.github.com/NucleoTeam/NucleoDB") // Replace 'OWNER' and 'REPO' with appropriate values
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    implementation(project(':shared'))
    implementation(project(':library'))
}

shadowJar {
    archiveBaseName.set(group)
    archiveVersion.set(version)
    archiveClassifier.set('')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar

            // Additional configuration, e.g., add artifacts, set up pom details, etc.
            pom {
                name = 'NucleoDB Restore Client'
                description = 'Restore for nucleodb'
                url = 'https://github.com/NucleoTeam/NucleoDB'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'firestar'
                        name = 'Nathaniel Davidson'
                        email = 'nathaniel.davidson@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/NucleoTeam/NucleoDB.git'
                    developerConnection = 'scm:git:ssh://github.com/NucleoTeam/NucleoDB.git'
                    url = 'https://github.com/NucleoTeam/NucleoDB/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/NucleoTeam/NucleoDB")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

application {
    mainClass = 'com.nucleocore.library.cli.DBRestore'
}


test {
    useJUnitPlatform()
}